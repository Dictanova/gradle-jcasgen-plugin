/*
* Copyright 2013-2014 Damien Raude-Morvan
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*      http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/
buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "org.apache.uima:uimaj-tools:${uimaVersion}"
}

task sourcesJar( type: Jar ) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar( type: Jar ) {
    classifier 'javadoc'
    from javadoc
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava( MavenPublication ) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = project.hasProperty( 'bintrayUser' ) ? project.getProperty( 'bintrayUser' ) : null
    key = project.hasProperty( 'bintrayKey' ) ? project.getProperty( 'bintrayKey' ) : null

    publications = ['mavenJava']
    publish = true

    pkg {
        repo = 'gradle-jcasgen-plugin'
        name = 'gradle-jcasgen-plugin'
        licenses = ['Apache-2.0']
        desc = 'Gradle plugin for generating UIMA type system beans from XML descriptors'
        websiteUrl = 'https://github.com/Dictanova/gradle-jcasgen-plugin'
        issueTrackerUrl = 'https://github.com/Dictanova/gradle-jcasgen-plugin/issues'
        vcsUrl = 'https://github.com/Dictanova/gradle-jcasgen-plugin.git'
        labels = ['gradle', 'uima', 'jcasgen']
        publicDownloadNumbers = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.1-20140717111533+0000"
}
